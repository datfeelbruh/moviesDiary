plugins {
	id 'java'
	id 'checkstyle'
	id 'application'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id "org.sonarqube" version "4.2.1.3168"
	id 'jacoco'
}

sonar {
	properties {
		property("sonar.projectKey", "moviesDiary")
		property("sonar.projectName", "moviesDiary")
		property("sonar.host.url", "http://localhost:9000")
	}
}

compileJava {
	options.encoding = "UTF-8"
}

test {
	systemProperty "file.encoding", "utf-8"
}

compileTestJava {
	options.encoding = "UTF-8"
}

group = 'sobad.code'
version = '1.0'

application {
	mainClass = "sobad.code.moviesdiary.MoviesDiaryApplication"
}

jar {
	manifest {
		attributes["Main-class"] = "sobad.code.movies_diary.MoviesDiaryApplication"
	}
}

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation (
			'org.hibernate.search:hibernate-search-mapper-orm-orm6:6.2.1.Final',
			'org.hibernate.search:hibernate-search-backend-lucene:6.2.1.Final',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-mail',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0',
			'org.liquibase:liquibase-core',
			'com.querydsl:querydsl-jpa:5.0.0:jakarta',
			'io.jsonwebtoken:jjwt-api:0.11.5',
			'io.jsonwebtoken:jjwt-jackson:0.11.5',
			'io.jsonwebtoken:jjwt-impl:0.11.5',
	)
	compileOnly (
			'org.projectlombok:lombok'
	)
	developmentOnly (
			'org.springframework.boot:spring-boot-devtools'
	)
	runtimeOnly (
			'org.postgresql:postgresql',
			'com.h2database:h2',
	)
	annotationProcessor (
			'org.projectlombok:lombok',
			'com.querydsl:querydsl-apt:5.0.0:jakarta',
			'jakarta.persistence:jakarta.persistence-api:3.1.0'
	)

	testAnnotationProcessor (
			'org.projectlombok:lombok'
	)

	testImplementation (
			'org.springframework.boot:spring-boot-starter-test',
			'org.springframework.security:spring-security-test',
			'org.springframework.boot:spring-boot-starter-security',
			'org.testcontainers:postgresql:1.18.3',
			'org.testcontainers:junit-jupiter:1.16.2',
			'org.projectlombok:lombok'
	)
	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core',
			'info.picocli:picocli:4.7.1',
			'org.liquibase.ext:liquibase-hibernate6:4.21.1',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.postgresql:postgresql',
			'com.h2database:h2'
	)
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			url 'jdbc:postgresql://localhost:5432/movies_diary'
			username "datfeel"
			password "asedur81"
			referenceUrl 'hibernate:spring:movies_diary.entities.' +
					'?dialect=org.hibernate.dialect.PostgreSQL10Dialect' +
					'&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy'
		}
	}
}

checkstyle {
	toolVersion "10.0"
}

diffChangelog {
	dependsOn compileJava
}

tasks.named('test') {
	useJUnitPlatform()
}



test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	reports {
		xml.required = true
		html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
	}
	dependsOn test
	afterEvaluate {
		classDirectories.from(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ['sobad/code/moviesdiary/entities/*'])
		}))
	}
}

jacoco {
	toolVersion = "0.8.9"
}






